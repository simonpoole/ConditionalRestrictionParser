/*
 */

plugins {
    id "ca.coglinc.javacc" version "2.4.0"
    id "org.sonarqube" version "4.0.0.2929"
}

// Apply the java plugin to add support for Java
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: "jacoco"


version = '0.3.4'
sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
    javaccSourcePath = project.buildDir.absolutePath + '/generated/javacc/'
    javaccOutput = file(javaccSourcePath)
    mainSource = file('src/main/java/')
    testSource = file('src/test/java/')
}

eclipse {
    classpath {
        file {
            beforeMerged { classpath ->
                classpath.entries.add(new org.gradle.plugins.ide.eclipse.model.SourceFolder(javaccSourcePath, "bin"))
            }
        }
    }
}

compileJavacc {
    inputDirectory = mainSource
    outputDirectory = javaccOutput
}

compileJava {
    source javaccOutput,mainSource
}

compileTestJava {
    source javaccOutput,mainSource,testSource
}

javadoc {
    source mainSource, testSource
    exclude '**/*.jj'
    failOnError = false
}

task sourcesJar(type: Jar) {
    from mainSource, testSource
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
    //	options.encoding = 'UTF-8'
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

sonarqube {
    properties {
        property "sonar.java.source", "1.8"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.java.binaries", "build/classes/java/main/"
        property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/jacocoTestReport/jacocoTestReport.xml"
        property "sonar.qualitygate.wait", "true"
    }
}

task updateTranslations(type: Exec) {
    if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
        commandLine 'cmd', '/c', 'tx.exe', 'pull', '-a'
    } else {
        commandLine 'tx', 'pull', '-a'
    }
}
updateTranslations.group = 'translation'
updateTranslations.description = 'Update translations by executing the transifex tx utility'

task replaceVersion {
    doLast {
        ant.replaceregexp(match:'ch\\.poole\\:ConditionalRestrictionParser\\:([0-9\\.]+)', replace:"ch.poole:ConditionalRestrictionParser:${version}", flags:'g', byline:true) {
            fileset(dir: '.', includes: 'README.md')
        }
    }
}
build.dependsOn replaceVersion

test {
    // test input may change and require rerunning the tests
    inputs.files("test-data/cr.txt")
    testLogging {
        outputs.upToDateWhen {false}
        showStandardStreams = true
    }
}

ext {
    repoUrl = 'https://github.com/simonpoole/ConditionalRestrictionParser.git'
    spdxId = 'MIT'
}

publishing {
    publications {
        Release(MavenPublication) {
            from components.java
            groupId 'ch.poole'
            artifactId 'ConditionalRestrictionParser'
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = artifactId
                description = 'This is a simple parser for OSM conditional restriction tags'    
                url = 'https://github.com/simonpoole/ConditionalRestrictionParser'
                scm {
                    url = repoUrl
                }
                licenses {
                    license {
                        name = spdxId
                        url = 'https://raw.githubusercontent.com/simonpoole/ConditionalRestrictionParser/master/LICENCE.txt'
                    }
                }
                developers {
                    developer {
                        name = 'Simon Poole'
                    }
                }
            }
            repositories {
                maven {
                    name = 'sonatype'
                    credentials {
                        username System.getenv('SONATYPE_USER')
                        password System.getenv('SONATYPE_PASSWORD')
                    }
                    def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                    def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                    url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                }
            }
        }
    }
}

signing {
    sign publishing.publications.Release
}

// In this section you declare where to find the dependencies of your project
repositories {
    mavenCentral()
}

// In this section you declare the dependencies for your production and test code
dependencies {
    implementation 'ch.poole:OpeningHoursParser:0.27.1'
    compileOnly 'org.jetbrains:annotations:19.0.0'
    testCompileOnly 'org.jetbrains:annotations:19.0.0'
    testImplementation 'junit:junit:4.12'
}


